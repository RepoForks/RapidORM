apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}


// ********************************** upload archives ***********************************

apply plugin: 'maven'
apply plugin: 'signing' //使用signing plugin做数字签名

//定义GroupID和Version，ArtefactID会自动使用Project名
group = "$archiveGroup"
version = "$archiveVersion"
//sourceCompatibility = 1.7
//targetCompatibility = 1.7

repositories {
    mavenCentral();
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
//    archives file("$archivePath")
    archives javadocJar, sourcesJar
}



signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            //为Pom文件做数字签名
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            //指定项目部署到的中央库地址，UserName和Password就是Part 1中注册的账号。
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: "$ossrhUsername", password: "$ossrhPassword")
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: "$ossrhUsername", password: "$ossrhPassword")
            }

            //构造项目的Pom文件，参见Part 2中Pom文件的规范，不要遗漏必填项
            pom.project {
                artifactId "$archiveName"
                name "$archiveName"
                packaging 'jar'
                description "$archiveDesc"
                url "$archiveUrl"

                scm {
                    url 'scm:git@github.com:wangjiegulu/RapidORM.git'
                    connection 'scm:git@github.com:wangjiegulu/RapidORM.git'
                    developerConnection 'git@github.com:wangjiegulu/RapidORM.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'wangjie'
                    }
                }

                developers {
                    developer {
                        id 'wangjie'
                        name 'Wang Jie'
                        email 'tiantian.china.2@gmail.com'
                    }
                }
            }
        }
    }
}

